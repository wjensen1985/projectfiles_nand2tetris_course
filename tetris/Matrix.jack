
/** Matrix class for 2d array of integer points */
class Matrix{
    field Array a;
    field int rows;
    field int cols;

    method void set_point(int r, int c, int value){
        var Array tmp;
        var int row_idx, col_idx;
        let row_idx = r - 1;
        let col_idx = c - 1;
        
        // check in bounds
        if ((row_idx > -1) & (row_idx < rows) & (col_idx > -1) & (col_idx < cols)) {
            let tmp = a[row_idx];
            // do Output.printInt(tmp[0]);
            let tmp[col_idx] = value;
        }else{
            // do nothing
        }

        return;
    }

    method int get(int r, int c){
        var int ret;
        var Array tmp;
        var int row_idx, col_idx;
        let row_idx = r - 1;
        let col_idx = c - 1;

        if ((row_idx > -1) & (row_idx < rows) & (col_idx > -1) & (col_idx < cols)) {
            let tmp = a[row_idx];
            let ret = tmp[col_idx];
        }else {
            // do nothing
            let ret = -1;
        }

        return ret;
    }

    method void set_row(int r, int value){
        var Array tmp;
        var int i;
        var int row_idx;
        let row_idx = r - 1;

        // check in bounds
        if ((row_idx > -1) & (row_idx < rows)) {
            let tmp = a[row_idx];
            let i = 0;
            while (i < cols) {
                let tmp[i] = value;
                let i = i + 1;
            }
        }else{
            // do nothing
        }

        return;
    }

    method void set_all(int value){
        var int r, c;
        var Array tmp;

        let r = 0;
        while (r < rows) {
            let tmp = a[r];
            let c = 0;
            while (c < cols){
                let tmp[c] = value;
                let c = c + 1;
            }
            let r = r + 1;
        }

        return;
    }

    constructor Matrix new(int init_rows, int columns, int init_val){
        var int r, c;
        var Array tmp;

        let rows = init_rows;
        let cols = columns;

        let a = Array.new(rows);
        let r = 0;
        
        while (r < rows){
            let a[r] = Array.new(cols);
            let tmp = a[r];
            let c = 0;
            while (c < cols){
                let tmp[c] = init_val;
                let c = c + 1;
            }
            let r = r + 1;
        }

        return this;
    }

    method void dispose(){
        var int r;
        var Array rowRef;

        let r = 0;
        while (r < rows){
            let rowRef = a[r];
            do rowRef.dispose();
            let r = r + 1;
        }

        do a.dispose();

        do Memory.deAlloc(this);
        return;
    }
}