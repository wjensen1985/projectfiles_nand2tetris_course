/** PieceBag class for piece 7-bag random piece selection. */

class PieceBag {
    field Array bag;
    field int idx;
    field int seed;

    method int rand() {
        let seed = Math.multiply(seed, 25173)+ 13849;
        let seed = (seed & 32767);
        return seed;
    }

    method int randRange(int n){
        var int q, r, s;
        if (n < 1){
            return 0;
        }
        let s = rand();
        let q = Math.divide(s, n);
        let r = s - Math.multiply(q, n);
        return r;
    }

    method void refillAndShuffle() {
        var int i, j, tmp;

        let bag[0] = 0;
        let bag[1] = 1;
        let bag[2] = 2;
        let bag[3] = 3;
        let bag[4] = 4;
        let bag[5] = 5;
        let bag[6] = 6;
        
        let i = 6;
        while (i > -1){
            let j = randRange(i + 1);
            let tmp = bag[i];
            let bag[i] = bag[j];
            let bag[j] = tmp;

            let i = i - 1;
        }

        let idx = 0;
        return;
    }

    method void reseed(int frameCount) {
        var int k, t;
        let k = Keyboard.keyPressed();
        let t = frameCount;
        let seed = seed + k + t;
        let seed = (seed & 32767);
        return;
    }

    method int next(){
        var int id;
        if (idx = 7){
            do refillAndShuffle();
        }
        let id = bag[idx];
        let idx = idx + 1;
        return id;
    }

    constructor PieceBag new(int frames){
        let bag = Array.new(7);
        let seed = 12345;
        do reseed(frames);
        do refillAndShuffle();

        return this;
    }

    method void dispose(){
        do bag.dispose();
        do Memory.deAlloc(this);
        return;
    }
}